buildscript {
    repositories {
        jcenter()
        maven { url = "https://maven.minecraftforge.net/" }
        maven {url = "https://repo.spongepowered.org/maven"}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.0.0"
group = "com.ikexing.randomtweaker"
archivesBaseName = "randomtweaker"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    def args = [
            "-Dfml.coreMods.load=ink.ikx.rt.impl.core.RTPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true"
    ]

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

dependencies {
    compile files('libs/')

    deobfCompile "mezz.jei:jei_1.12.2:4.16.1.301"
    deobfCompile "vazkii.botania:Botania:r1.10-363.148"
    deobfCompile "curse.maven:thaumcraft-223628:2629023"
    deobfCompile "curse.maven:toughasnails-246391:2710969"
    deobfCompile "curse.maven:astralsorcery-241721:3044416"
    deobfCompile "curse.maven:thebetweenlands-243363:3248202"
    deobfCompile "com.teamacronymcoders:ContentTweaker:1.12.2-4.9.1"
    deobfCompile "com.teamacronymcoders.base:base:1.12.2-3.13.0-SNAPSHOT.+"

    runtime "curse.maven:mixin-booter-419286:3321174"
    runtime "curse.maven:Baubles-227083:2518667"
    runtime "curse.maven:zenutils-401178:3303426"
    runtime "cofh:RedstoneFlux:1.12-2.0.0.1:universal"
    runtime "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"

    compile('org.spongepowered:mixin:0.8') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'jei'
        url =  "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = 'blamejared'
        url = 'https://maven.blamejared.com'
    }
    maven {
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }
}

jar {
    manifest {
        attributes([
                'TweakClass' : 'org.spongepowered.asm.launch.MixinTweaker',
                "ForceLoadAsMod" : true,
                "FMLCorePlugin": "ink.ikx.rt.impl.core.RTPlugin",
                "FMLCorePluginContainsFMLMod": true,
        ])
    }
}

mixin {
    add sourceSets.main, "mixins.randomtweaker.refmap.json"
}

repositories.each {
    if (it instanceof MavenArtifactRepository && it.url.toString() == "https://files.minecraftforge.net/maven") {
        it.url = "https://maven.minecraftforge.net/"
    }
}